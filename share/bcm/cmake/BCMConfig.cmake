
list(APPEND CMAKE_MODULE_PATH ${CMAKE_CURRENT_LIST_DIR})
include(BCMFuture)
enable_testing()

function(define_find_package INPUT_PROJECT_NAME)
    macro(find_package NAME)
        if(NOT "${NAME}" MATCHES "^${CMAKE_PROJECT_NAME}_.*$" AND NOT "${NAME}" STREQUAL BCM)
            _find_package(${ARGV})
        else()
            set(${ARGV0}_FOUND ON CACHE BOOL "")
        endif()
    endmacro()
endfunction()

function(find_subdirectories INPUT_DIRECTORY SUBMODULE_HEADER)
    file(GLOB_RECURSE LIBS ${INPUT_DIRECTORY}/*CMakeLists.txt)
    foreach(lib ${LIBS})
        file(READ ${lib} CONTENT)
        if("${CONTENT}" MATCHES ${SUBMODULE_HEADER})
            get_filename_component(LIB_DIR ${lib} DIRECTORY)
            get_filename_component(LIB_NAME ${LIB_DIR} NAME)
            if(NOT "${LIB_NAME}" IN_LIST EXCLUDE_LIBS)
                add_subdirectory(${LIB_DIR})
            endif()
        endif()
    endforeach()
endfunction()